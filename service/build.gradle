plugins {
    id 'org.springframework.boot' version '1.5.11.RELEASE'
    id 'application'
}

jar {
    baseName = "sbjb"
    group = 'com.bnc'
    version = rootProject.version
    manifest {
        attributes "Class-Path": "lib/*.jar"
        attributes "Project-Version": rootProject.version
        attributes "Main-Class": "com.bnc.sbjb.Application"
    }
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '1.5.11.RELEASE'
    implementation group: 'io.dropwizard.metrics', name: 'metrics-servlets', version: '3.1.0'
    implementation group: 'io.dropwizard.metrics', name: 'metrics-jvm', version: '3.1.0'

    // Testing dependencies
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '1.5.11.RELEASE'
}

processResources {
    filesMatching("**/version.properties") {
        def nowUtc = java.time.ZonedDateTime.now(java.time.ZoneId.of("UTC"))
        def nowNz = java.time.ZonedDateTime.now(java.time.ZoneId.of("Pacific/Auckland"))
        def projectBranch = "BRANCH not provided"
        if (project.hasProperty("BRANCH")) {
            projectBranch = project.property("BRANCH")
        }
        def projectCommitTag = "TAG not provided"
        if (project.hasProperty("TAG")) {
            projectCommitTag = project.property("TAG")
        }
        printf("processing resources TAG is [%s]\n", projectCommitTag)
        printf("processing resources BRANCH is [%s]\n", projectBranch)
        printf("UTC time is [%s]\n", nowUtc)
        printf("Auckland time is [%s]\n", nowNz)
        expand(
                "projectVersion": rootProject.version,
                "gitCodeVersion": projectCommitTag,
                "gitBranch": projectBranch,
                "buildTimestamp": nowNz.toString(),
                "buildTimestampUtc": nowUtc.toString()
        )
    }
}
